(ns shrine.login
  (:require ["package:flutter/material.dart" :as m]
            [cljd.flutter :as f]))

(defn ^m/Widget login-page
  [ctx]
  (f/widget
   :context ctx
   :let [username-controller (m/TextEditingController)
         password-controller (m/TextEditingController)]
   (m/Scaffold
    .body
    (m/SafeArea
     .child
     (m/ListView
      .padding (m/EdgeInsets.symmetric .horizontal 24.0)
      .children [(m/SizedBox .height 80.0)
                 (m/Column
                  .children
                  [(m/Image.asset "assets/diamond.png")
                   (m/SizedBox .height 16.0)
                   (m/Text "SHRINE")])
                 (m/SizedBox .height 120.0)
                 (m/TextField
                  .controller username-controller
                  .decoration (m/InputDecoration .filled true .labelText "Username"))
                 (m/SizedBox .height 12.0)
                 (m/TextField
                  .controller password-controller
                  .decoration (m/InputDecoration .filled true .labelText "Password")
                  .obscureText true)
                 (m/OverflowBar
                  .alignment m/MainAxisAlignment.end
                  .children [(m/TextButton
                              .child (m/Text "CANCEL")
                              .onPressed
                              (fn [] (doall
                                      (map #(.clear ^m/TextEditingController %)
                                           [username-controller
                                            password-controller]))))
                             (m/ElevatedButton
                              .child (m/Text "NEXT")
                              .onPressed #(.pop m/Navigator ctx))])])))))
